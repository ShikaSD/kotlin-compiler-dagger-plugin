plugins {
    id 'com.gradle.plugin-publish' version '0.10.1'
}

apply plugin: "java-gradle-plugin"
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "maven"

group "me.shika.di"
version "0.0.2-preview-r2"

configurations {
    embedded {
        transitive = false
    }
    compileOnly.extendsFrom(embedded)
}

pluginBundle {
    website = 'https://github.com/ShikaSD/kotlin-compiler-di'
    vcsUrl = 'https://github.com/ShikaSD/kotlin-compiler-di.git'
    tags = ['kotlin', 'compiler-plugin', 'di', 'dagger2']

    plugins {
        diPlugin {
            displayName = "Dagger 2 Kotlin compiler plugin"
            description = "Experiment on implementation of dagger2 code generation through Kotlin compiler"
        }
    }

    mavenCoordinates {
        groupId = project.getGroup()
        artifactId = "dagger-compiler-plugin"
        version = project.getVersion()
    }
}

install {
    repositories {
        mavenInstaller {
            pom.artifactId = 'dagger-compiler-plugin'
        }
    }
}

gradlePlugin {
    plugins {
        diPlugin {
            id = "me.shika.dagger-compiler-plugin"
            implementationClass = "me.shika.di.DiCompilerPlugin"
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin-api"
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin"

    embedded project(':compiler-plugin:kotlin-plugin')

    embeddedProjects().collect { it.configurations.findByName("published") }
        .forEach {
            it.dependencies.forEach {
                runtime "$it.group:$it.name:$it.version"
            }
        }
}

jar {
    from project.configurations.embedded.collect {
        zipTree(it)
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from embeddedProjects().collect { it.sourceSets.main.allSource }
}

artifacts {
    archives sourcesJar
}

def embeddedProjects() {
    return project.configurations.findByName('embedded')
        .resolvedConfiguration
        .resolvedArtifacts
        .findAll { it.id.componentIdentifier instanceof ProjectComponentIdentifier }
        .collect { project(it.id.componentIdentifier.projectPath) }
}

