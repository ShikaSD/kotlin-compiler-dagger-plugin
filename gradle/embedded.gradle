configurations {
    embedded {
        transitive = false
    }
    implementation.extendsFrom(embedded)

    published
    implementation.extendsFrom(published)
}

def embeddedProjects() {
    return project.configurations.findByName('embedded')
        .resolvedConfiguration
        .resolvedArtifacts
        .findAll { it.id.componentIdentifier instanceof ProjectComponentIdentifier }
        .collect { project(it.id.componentIdentifier.projectPath) }
}

afterEvaluate {
    jar {
        from project.configurations.embedded.collect {
            zipTree(it)
        }
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from embeddedProjects().collect { it.sourceSets.main.allSource }
    }

    dependencies {
        embeddedProjects().collect { it.configurations.findByName("published") }
            .forEach {
                if (it == null) return
                it.dependencies.forEach {
                    runtimeOnly "$it.group:$it.name:$it.version"
                }
            }
    }

    artifacts {
        archives sourcesJar
    }
}
